/*
 * SPDX-License-Identifier: GPL-3.0-or-later
 * Copyright (c) 2024-2026. The LibreFit Contributors
 *
 * LibreFit is subject to additional terms covering author attribution and trademark usage;
 * see the ADDITIONAL_TERMS.md and TRADEMARK_POLICY.md files in the project root.
 */

import org.gradle.api.JavaVersion.VERSION_17
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.compose.compiler)
    alias(libs.plugins.ksp)
    alias(libs.plugins.about.libraries)
}


android {
    namespace = "org.librefit"

    compileSdk {
        version = release(37) {
            minorApiLevel = 2
        }
    }


    buildFeatures {
        buildConfig = true
    }

    lint {
        baseline = file("lint-baseline.xml")
        checkGeneratedSources = false

        warning += listOf("MissingTranslation")
    }

    defaultConfig {
        applicationId = "org.librefit.app"
        minSdk = 26
        targetSdk = 37

        versionName = "0.5.0"
        versionCode = 50001

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
        androidResources {
            generateLocaleConfig = true
            localeFilters += setOf(
                "en", "it", "de", "nl", "es", "cs", "zh-rCN", "pt-rBR", "ru", "fr"
            )
        }

        // Do not use System.currentTimeMillis()
        val timestamp = System.getenv("SOURCE_DATE_EPOCH")?.toLongOrNull() ?: 1700000000L
        buildConfigField("long", "BUILD_TIME", "${timestamp}L")

        // Disable vector->PNG generation (legacy adapter), which is non-deterministic
        vectorDrawables.generatedDensities()
    }

    buildTypes {
        release {
            // Disable VCS Info (AGP 8.3+).
            // If the git repo isn't perfectly clean, this injects diffs
            vcsInfo.include = false

            // It is a common source of instability
            isShrinkResources = true

            isMinifyEnabled = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-debug"
        }
        sourceSets {
            named("release") {
                kotlin.directories += "build/generated/ksp/release/kotlin"
                kotlin.directories += "build/generated/ksp/release/java"
            }
            named("debug") {
                kotlin.directories += "build/generated/ksp/debug/kotlin"
                kotlin.directories += "build/generated/ksp/debug/java"
            }
        }
    }
    compileOptions {
        sourceCompatibility = VERSION_17
        targetCompatibility = VERSION_17
    }
    buildFeatures {
        compose = true
    }
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }

        jniLibs {
            // Prevents AGP from modifying pre-compiled .so files from dependencies
            keepDebugSymbols.add("**/*.so")
        }
    }
}

base {
    archivesName.set("LibreFit")
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas")
}

kotlin {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_17
    }
}

dependencyLocking {
    lockAllConfigurations()
}

dependencies {

    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.activity.compose)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.ui)
    implementation(libs.androidx.ui.graphics)
    implementation(libs.androidx.ui.tooling.preview)
    implementation(libs.androidx.material3)
    implementation(libs.androidx.appcompat)
    implementation(libs.kotlinx.serialization.json)

    // Unit test
    // kotlin-test-junit: multiplatform kotlin-test API compiled onto the JUnit 4 runner;
    // brings kotlin-test + junit transitively. Explicit artifact because AGP built-in Kotlin
    // does not drive kotlin-test's framework-variant auto-selection.
    testImplementation(libs.kotlin.test.junit)
    testImplementation(libs.kotlinx.coroutines.test)
    testImplementation(libs.turbine)
    // Truth assertions, still used by the fork's own tests
    testImplementation(libs.androidx.truth)
    testImplementation(libs.mockk.android)
    testImplementation(libs.mockk.agent)
    testImplementation(libs.koin.test)

    // Instrumented test
    // kotlin-test-junit compiles kotlin.test annotations to JUnit 4 for AndroidJUnitRunner
    androidTestImplementation(libs.kotlin.test.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.ui.test.junit4)


    debugImplementation(libs.androidx.ui.tooling)
    debugImplementation(libs.androidx.ui.test.manifest)

    // Navigation 3
    implementation(libs.androidx.navigation3.runtime)
    implementation(libs.androidx.navigation3.ui)

    // Scopes a ViewModelStore to each NavEntry (required for koinViewModel() per destination)
    implementation(libs.androidx.lifecycle.viewmodel.navigation3)

    // Room
    implementation(libs.androidx.room.runtime)
    annotationProcessor(libs.androidx.room.compiler)
    ksp(libs.androidx.room.compiler)

    // Kotlin Extensions and Coroutines support for Room
    implementation(libs.androidx.room.ktx)

    // DataStore
    implementation(libs.androidx.datastore.core.android)
    implementation(libs.androidx.datastore.preferences)

    // Lottie animations for jetpack compose
    implementation(libs.lottie.compose)

    // Splash screen API
    implementation(libs.androidx.core.splashscreen)

    // M3 Compose vico charts
    implementation(libs.compose.m3)

    // Koin for dependency injection
    implementation(libs.koin.android)
    implementation(libs.koin.androidx.compose)


    // AboutLibraries to show used dependencies in jetpack compose
    implementation(libs.aboutlibraries.compose.m3)

    // Used in ui models' properties
    implementation(libs.kotlinx.collections.immutable)

    // Used to apply material colors to splash screen
    implementation(libs.material)

    // Load images asynchronously
    implementation(libs.coil)

    // Reorder items in edit workout/routine lists
    implementation(libs.reorderable)
}
